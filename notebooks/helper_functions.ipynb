{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Various helper functions"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Fit calibration"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using LsqFit ## a package for least square fitting\n",
    "\"\"\"\n",
    "    fit_calibration(bucketsize, solution, calis...)\n",
    "\n",
    "Fits a line of best fit through the calibration data going through the origin.\n",
    "Returns the function of this line: `f(cond-cond_at_0) -> concentration`.\n",
    "\n",
    "Also prints the parameters values +/- 95% confidence intervals.\n",
    "\n",
    "Uses the package LsqFit: https://github.com/JuliaOpt/LsqFit.jl\n",
    "\"\"\"\n",
    "function fit_calibration(bucketsize, solution, calis...)\n",
    "    # subtract readout at zero concentration\n",
    "    for c in calis\n",
    "        @assert c[1,1]==0 \"First row needs to be zero reading!\"\n",
    "        c[:,2] .= c[:,2].-c[1,2]\n",
    "    end\n",
    "    # concatenate all calibrations\n",
    "    cali = vcat(calis...)\n",
    "    conc = ml_to_concentration(cali[:,1], solution, bucketsize)\n",
    "    delta_readout = cali[:,2]\n",
    "    # Fit line using https://github.com/JuliaOpt/LsqFit.jl\n",
    "    fn(delta_readout, p) = p[1]*delta_readout ## p[1]==a, p[2]==b\n",
    "    para_weights = [0.5] ## equal weights to parameters\n",
    "    fit = curve_fit(fn, delta_readout, conc, para_weights)\n",
    "    errors = margin_error(fit, 1-0.95)\n",
    "    println(\"\"\"\n",
    "    Estimated linear fit: f(delta_cond) = a*conc with\n",
    "     a = $(round(fit.param[1],sigdigits=3))±$(round(errors[1],sigdigits=3))\n",
    "    \"\"\")\n",
    "    return (delta_readout) -> fn(delta_readout, fit.param)\n",
    "end;"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Read Keller sensors"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "Main.##281.cut_sensor_readout"
     },
     "metadata": {},
     "execution_count": 2
    }
   ],
   "cell_type": "code",
   "source": [
    "using Dates\n",
    "using DelimitedFiles: readdlm, writedlm ## Date time handling; CSV file handling\n",
    "const g = 9.81\n",
    "const rhow = 1000.0\n",
    "\n",
    "\"\"\"\n",
    "         read_Keller(filename;\n",
    "                     presshead=\"P1\",\n",
    "                     condhead=\"ConRaw\",\n",
    "                     temphead=\"TOB1\",\n",
    "                     skipstart=8,\n",
    "                     )\n",
    "\n",
    "Reads a Keller pressure/CTD sensor.  However, you probably want to use\n",
    "- `read_Keller_DCX22_CTD`,\n",
    "- `read_Keller_DCX22` and\n",
    "- `read_Keller_DC22`\n",
    "\n",
    "Returns a dict with keys as appropriate:\n",
    "- :t [date-time stamp]\n",
    "- :cond [μS/cm]\n",
    "- :temp [C]\n",
    "- :press [m H2O]\n",
    "\"\"\"\n",
    "function read_Keller(filename;\n",
    "                     presshead=\"P1\",\n",
    "                     condhead=\"ConRaw\",\n",
    "                     temphead=\"TOB1\",\n",
    "                     skipstart=8,\n",
    "                     )\n",
    "    d,h = readdlm(filename, ';', skipstart=skipstart, header=true)\n",
    "    h = h[:] ## h is a 1x2 matrix, change to a vector\n",
    "\n",
    "    out = Dict{Symbol,Any}()\n",
    "    # find date-time rows\n",
    "    id, it = findfirst(h.==\"Date\"), findfirst(h.==\"Time\")\n",
    "    # time 12.08.2016 13:36:58\n",
    "    fmtd, fmtt = \"d/m/y\", \"H:M:S\"\n",
    "    out[:t] = [Date(dd, fmtd) + Time(tt, fmtt) for (dd,tt) in zip(d[:,id], d[:,it])]\n",
    "\n",
    "    for (head, key) in [(presshead, :press),\n",
    "                        (condhead, :cond),\n",
    "                        (temphead, :temp)]\n",
    "        i = findfirst(h.==head) ## see if there is one\n",
    "        tmp = Float64[]\n",
    "        if i!=nothing\n",
    "            out[key] = [s==\"\" ? missing :\n",
    "                        s isa AbstractString ? parse(Float64, replace(s, \",\"=>\".\")) : Float64(s) for s in d[:,i]]\n",
    "            # convert mS/cm to μS/cm\n",
    "            if key==:cond\n",
    "                out[:cond] = out[:cond].*1000\n",
    "            end\n",
    "            # convert pressure from mbar to m H2O\n",
    "            if key==:press\n",
    "                out[:press] = out[:press]*1e2 /g/rhow\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # check lengths and remove all \"missing\"\n",
    "    l = length(out[:t])\n",
    "    topurge = []\n",
    "    for v in values(out)\n",
    "        @assert length(v)==l\n",
    "        append!(topurge, findall(v.===missing))\n",
    "    end\n",
    "    topurge = sort(unique(topurge))\n",
    "    for (k,v) in out\n",
    "        deleteat!(v, topurge)\n",
    "        if k!=:t\n",
    "            out[k] = Float64.(v) ## make the vector an\n",
    "        end\n",
    "    end\n",
    "    return out\n",
    "end\n",
    "\n",
    "read_Keller_DCX22_CTD(filename) = read_Keller(filename)\n",
    "read_Keller_DCX22(filename) = read_Keller(filename, error(\"Not implement yet\"))\n",
    "\n",
    "\"\"\"\n",
    "    cut_sensor_readout(sensor_readout, tinj, tend)\n",
    "\n",
    "Cuts the time series into individual tracer experiments.\n",
    "\"\"\"\n",
    "function cut_sensor_readout(sensor_readout, tinj, tend)\n",
    "\n",
    "    iinj = findfirst(sensor_readout[:t].>tinj)\n",
    "    iend = findfirst(sensor_readout[:t].>tend)-1\n",
    "    out = Dict()\n",
    "    for (k,v) in sensor_readout\n",
    "        if v isa Vector\n",
    "            out[k] = v[iinj:iend]\n",
    "        else\n",
    "            out[k] = v\n",
    "        end\n",
    "    end\n",
    "    # add time in secs since injection\n",
    "    t = sensor_readout[:t]\n",
    "    tt = convert(Vector{Float64}, Dates.value.(t[iinj:iend]-t[iinj])/1000)\n",
    "    out[:tsec] = tt\n",
    "    return out\n",
    "end"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.1"
  },
  "kernelspec": {
   "name": "julia-1.7",
   "display_name": "Julia 1.7.1",
   "language": "julia"
  }
 },
 "nbformat": 4
}
